/* 
* This program scans ports D0 to D7 on an ESP8266 and searches for I2C device.
* i2c_scanner
*
* Version 1
*    This program (or code that looks like it)
*    can be found in many places.
*    For example on the Arduino.cc forum.
*    The original author is not know.
* Version 2, Juni 2012, Using Arduino 1.0.1
*     Adapted to be as simple as possible by Arduino.cc user Krodal
* Version 3, Feb 26  2013
*    V3 by louarnold
* Version 4, March 3, 2013, Using Arduino 1.0.3
*    by Arduino.cc user Krodal.
*    Changes by louarnold removed.
*    Scanning addresses changed from 0...127 to 1...119,
*    according to the i2c scanner by Nick Gammon
*    http:*www.gammon.com.au/forum/?id=10896
* Version 5, March 28, 2013
*    As version 4, but address scans now to 127.
*    A sensor seems to use address 120.
* Version 6, November 27, 2015.
*    Added waiting for the Leonardo serial communication.
* Version 7, March 14, 2017
*    Addeded code to scan from D1 - D7
*    It puts an exception for D8 ie. GPIO15
* This sketch tests the standard 7-bit addresses
* Devices with higher bit address might not be seen properly.
*
*/

#include <Wire.h>

void setup() {
  Serial.begin(115200);
  while (!Serial);             // Leonardo: wait for serial monitor
  Serial.println("\n\nI2C Scanner to scan for devices on each port pair");
  scanPorts();
}

uint8_t portArray[] = {16, 5, 4, 0, 2, 14, 12, 13};
//String portMap[] = {"D0", "D1", "D2", "D3", "D4", "D5", "D6", "D7"}; //for Wemos
String portMap[] = {"GPIO16", "GPIO5", "GPIO4", "GPIO0", "GPIO2", "GPIO14", "GPIO12", "GPIO13"};

void scanPorts() { 
  for (uint8_t i = 0; i < sizeof(portArray); i++) {
    for (uint8_t j = 0; j < sizeof(portArray); j++) {
      if (i != j){
        Serial.print("Scanning (SDA : SCL) - " + portMap[i] + " : " + portMap[j] + " - ");
        Wire.begin(portArray[i], portArray[j]);
        check_if_exist_I2C();
      }
    }
  }
}

void check_if_exist_I2C() {
  byte error, address;
  int nDevices;
  nDevices = 0;
  for (address = 1; address < 127; address++ )  {
    // The i2c_scanner uses the return value of
    // the Write.endTransmisstion to see if
    // a device did acknowledge to the address.
    Wire.beginTransmission(address);
    error = Wire.endTransmission();

    if (error == 0){
      Serial.print("I2C device found at address 0x");
      if (address < 16)
        Serial.print("0");
      Serial.print(address, HEX);
      Serial.println("  !");

      nDevices++;
    } else if (error == 4) {
      Serial.print("Unknow error at address 0x");
      if (address < 16)
        Serial.print("0");
      Serial.println(address, HEX);
    }
  } //for loop
  if (nDevices == 0)
    Serial.println("No I2C devices found");
  else
    Serial.println("**********************************\n");
  //delay(1000);           // wait 1 seconds for next scan, did not find it necessary
}

void loop() {
}
